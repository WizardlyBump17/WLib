plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'maven-publish'
}

apply plugin: 'maven-publish'

allprojects {
    group = 'com.wizardlybump17.wlib'
    version = '1.5.1'

    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'java'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    sourceCompatibility = '16'
    targetCompatibility = '16'

    tasks {
        compileJava {
            options.encoding = 'UTF-8'
        }
    }

    java {
        withSourcesJar()
    }
}

subprojects {
    apply plugin: 'maven-publish'

    if (project.name == 'versions' || project.name.matches('v\\d_\\d+_R\\d+'))
        return

    publishing {
        repositories {
            maven {
                name = 'GitHubPackages'
                url = uri('https://maven.pkg.github.com/wizardlybump17/wlib')
                credentials {
                    username = project.findProperty('gpr.user') ?: System.getenv('USERNAME')
                    password = project.findProperty('gpr.key') ?: System.getenv('TOKEN')
                }
            }
        }

        publications {
            gpr(MavenPublication) {
                from(components.java)
            }
        }
    }
}